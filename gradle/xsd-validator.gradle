task unzipXsd(type: Copy) {
    from zipTree(configurations.xsd.singleFile).matching {
        include 'findbugsplugin.xsd'
        include 'messagecollection.xsd'
    }
    into "$buildDir/xsd"
}

task validateFindBugsXml {
    doFirst {
        ant {
            schemavalidate (
                    file: 'src/main/resources/findbugs.xml',
                    noNamespaceFile: "$buildDir/xsd/findbugsplugin.xsd"
                    ) {
                        schema(
                                namespace: "http://www.w3.org/2001/XMLSchema-instance",
                                file: "$buildDir/xsd/findbugsplugin.xsd"
                                )
                    }
        }
    }
    dependsOn unzipXsd
}

task validateMessageXml {
    doFirst {
        ant {
            schemavalidate (
                    file: 'src/main/resources/messages.xml',
                    noNamespaceFile: "$buildDir/xsd/messagecollection.xsd"
                    ) {
                        schema (
                                namespace: "http://www.w3.org/2001/XMLSchema",
                                file: "$buildDir/xsd/messagecollection.xsd"
                                )
                    }
        }
    }
    dependsOn unzipXsd
}

task validateXml {
    dependsOn validateFindBugsXml
    dependsOn validateMessageXml
}

tasks.check.dependsOn validateXml
